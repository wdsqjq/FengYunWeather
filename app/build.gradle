plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
//    id 'per.wsj.plugin.PublishPlugin'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "me.wsj.fengyun"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 13
        versionName "1.1.3"

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //指定room.schemaLocation生成的文件路径
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        // 过滤语言包
        resConfigs('zh-rCN', 'en')
    }

    signingConfigs {
        debug {
            if (configProperties != null && configProperties.get("filename") != null){
                storeFile file(configProperties.get("filename"))
                storePassword configProperties.get("storePassword")
                keyAlias configProperties.get("keyAlias")
                keyPassword configProperties.get("keyPassword")
            }
        }
    }

    buildTypes {
        release {
            zipAlignEnabled true
            shrinkResources true
            minifyEnabled true
            manifestPlaceholders = [amap_app_key: configProperties.get("gao_de_app_key")]
            buildConfigField('String', 'HeFengKey', configProperties.get("he_feng_app_key"))
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
            // todo 请在local.properties中配置天气及定位的api key
            manifestPlaceholders = [amap_app_key: configProperties.get("gao_de_app_key")]
            buildConfigField('String', 'HeFengKey', configProperties.get("he_feng_app_key"))
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            /*output.processResources.doFirst { pm ->
                String manifestPath = output.processResources.manifestFile
                println "=====test manifestPath=====$manifestPath"
                def manifestContent = file(manifestPath).getText()
                def xml = new XmlParser().parseText(manifestContent)
//                println "=====test xml=====$xml"
                // 增加一个meta-data节点
//                xml.application[0].appendNode("meta-data", ['android:name': 'channel', 'android:value': 'yingyongbao'])
                // 增加一个属性
//                xml.application[0].attributes().put("android:persistent", "true")

                def serialize = groovy.xml.XmlUtil.serialize(xml)
                file(manifestPath).write(serialize)
            }*/
            if (variant.buildType.name == 'release') {
                def fileName = "fengyun-weather-" + defaultConfig.versionName + ".apk"
                outputFileName = new File("", fileName)
            } else if (variant.buildType.name == 'debug') {
//                fileName = "fengyun-weather-debug-" + ".apk"
            }
        }
    }

    /*gradle.buildFinished { var1 ->
        println var1.getAction()
        println '构建结束 buildFinished'
    }*/
}

dependencies {
    implementation project(path: ':lib')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.lifecycle:lifecycle-service:2.2.0'

    implementation files('libs/AMap_Location_V5.3.1_20210331.jar')

    implementation 'com.google.dagger:hilt-android:2.36'
    kapt 'com.google.dagger:hilt-compiler:2.36'

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

//    debugImplementation  'com.github.wsj1024:CrashCanary:1.0.4'
//
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'
}